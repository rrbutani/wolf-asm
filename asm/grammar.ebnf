(* The grammar of the wolf assembly language.
 *
 * This file is not guaranteed to be fully up to date with the parser. The code
 * should be treated as the source of truth for what the actual grammar is.
 *)

program ::= stmt*
stmt ::= label* stmt_body? newline
stmt_body ::= section_header | include | const | static_data | instr

label ::= ident ':'
section_header ::= 'section' ('.static' | '.code')

include ::= '.include' bytes_lit
const ::= '.const' ident immediate

static_data ::= static_bytes | static_zero | static_uninit | static_byte_string
static_bytes ::= ('.b1' | '.b2' | '.b4' | '.b8') immediate
static_zero ::= '.zero' integer_lit
static_uninit ::= '.uninit' integer_lit
static_byte_string ::= '.bytes' bytes_lit

(* Trailing commas are not allowed *)
instr ::= ident ((instr_arg ',')* instr_arg)?
instr_arg ::= offset_register | register | immediate | ident
offset_register ::= immediate '(' register ')'
immediate ::= integer_lit
